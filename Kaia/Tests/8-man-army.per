(defconst no 0)
(defconst yes 1)

(defconst g-enable-build 1)
(defconst g-enable-train 2)
(defconst g-enable-research 3)

(defconst g-feudal-rush 20)
(defconst g-fast-castle 21)

(defconst g-train-militiaman-line 100)
(defconst g-train-archer-line 101)

(defconst t-stop-attack 1)

(load "Kaia/Tests/research")


; build 1-6 barracks, archery range, stable based on age
; build 1-2 siege workshops based on age
; train knights (with flag)
; train trebuchets (with flag)
; train archers (with flag)
; train battering ram (with flag)
; trading
; resign
; research tech
; research unit upgrades only when there is a unit
; increase builder queues
; build walls


; globals
(defrule
    (true)
    =>
    (set-strategic-number sn-do-not-scale-for-difficulty-level 1) ; stop scaling
    (set-strategic-number sn-consecutive-idle-unit-limit 1) ; gives a new task faster
    (set-strategic-number sn-enable-patrol-attack 1) ; retarget against nearby sighted units
    (set-strategic-number sn-wall-targeting-mode 1) ; attack walls
    (set-strategic-number sn-gather-defense-units 1) ; gather soldiers near town center
    (set-strategic-number sn-enable-training-queue 15) ; number of units that can be queue'd at a building
    (set-strategic-number sn-enable-research-queue 1) ; allows technologies to be queued too
    (set-strategic-number sn-cap-civilian-builders 200) ; allow all civilians to build at the same time
    (set-strategic-number sn-scale-minimum-attack-group-size 0) ; fixes that min > max, which breaks attack groups
    (set-strategic-number sn-maximum-hunt-drop-distance 10) ; prevent hunting all over the map
    (set-strategic-number sn-dropsite-separation-distance 6) ; prevent tons of mining camps
    (set-strategic-number sn-initial-exploration-required 0) ; can build immediately
    (set-strategic-number sn-total-number-explorers 1)
	(set-strategic-number sn-number-explore-groups 1)
    (set-strategic-number sn-object-repair-level 14335) ; https://airef.github.io/strategic-numbers/sn-details.html#sn-object-repair-level
    (set-strategic-number sn-defer-dropsite-update 1) ; if camp is build at enemy; only "sacrafice" one villager
    (enable-wall-placement 1)
    (set-strategic-number sn-gate-type-for-wall 1) ; stone = 0, palaside = 1
    (set-goal g-feudal-rush yes)
    (set-goal g-fast-castle yes)
    (disable-self)
)

(defrule
    (true)
    =>
    (set-strategic-number sn-enable-new-building-system 1) ; use the new building system
    (up-assign-builders c: farm c: 1)
    (up-assign-builders c: house c: 1)
    (up-assign-builders c: town-center-foundation c: 1)
    (up-assign-builders c: mill c: 1)
    (up-assign-builders c: mining-camp c: 1)
    (up-assign-builders c: lumber-camp c: 1)
    (up-assign-builders c: dock c: 1)
    (up-assign-builders c: barracks c: 1)
    (up-assign-builders c: archery-range c: 1)
    (up-assign-builders c: stable c: 1)
    (up-assign-builders c: blacksmith c: 1)
    (up-assign-builders c: market c: 1)
    (up-assign-builders c: stone-wall-line c: 1)
    (up-assign-builders c: watch-tower c: 1)
    (up-assign-builders c: guard-tower c: 1)
    (up-assign-builders c: keep c: 1)
    (up-assign-builders c: donjon c: 1)
    (up-assign-builders c: gate c: 1)
    (up-assign-builders c: monastery c: 1)
    (up-assign-builders c: university c: 1)
    (up-assign-builders c: siege-workshop c: 1)
    (up-assign-builders c: castle c: 1)
    (up-assign-builders c: krepost c: 1)
    (up-assign-builders c: bombard-tower c: 1)
    (up-assign-builders c: feitoria c: 1)
    (up-assign-builders c: wonder c: 1)
    (disable-self)
)

(defrule
    (true)
    =>
    (set-strategic-number sn-camp-max-distance 25) ; default = 25
    (set-strategic-number sn-minimum-town-size 8) ; default = 12
    (set-strategic-number sn-maximum-town-size 14) ; default = 20
    (disable-self)
)

; escrow
(defrule
    (true)
    =>
    (set-escrow-percentage wood 30)
    (set-escrow-percentage food 30)
    (set-escrow-percentage gold 30)
    (set-escrow-percentage stone 30)
    (up-release-escrow)
    (disable-self)
)
(defrule
    (or (or (escrow-amount wood >= 800) 
            (escrow-amount food >= 800))
        (or (escrow-amount gold >= 800)
            (escrow-amount stone >= 800))
    )
    =>
    (up-release-escrow)
)

; increase town size
(defrule
    (current-age == castle-age)
    =>
    (set-strategic-number sn-minimum-town-size 14) ; default = 12
    (set-strategic-number sn-maximum-town-size 18) ; default = 20
    (disable-self)
)
(defrule
    (current-age >= castle-age)
    (building-type-count-total town-center >= 4)
    =>
    (set-strategic-number sn-minimum-town-size 18) ; default = 12
    (set-strategic-number sn-maximum-town-size 30) ; default = 20
    (disable-self)
)

; increase max camp size over time
; (defrule
;     (game-time > 900)
;     =>
;     (set-strategic-number sn-camp-max-distance 37)
;     (chat-local-to-self "sn-camp-max-distance = 37")
;     (disable-self)
; )
; (defrule
;     (game-time > 1800)
;     =>
;     (set-strategic-number sn-camp-max-distance 50)
;     (chat-local-to-self "sn-camp-max-distance = 50")
;     (disable-self)
; )
; (defrule
;     (game-time > 3600)
;     =>
;     (set-strategic-number sn-camp-max-distance 100)
;     (chat-local-to-self "sn-camp-max-distance = 100")
;     (disable-self)
; )

; assign gathering percentages based on population size
(defrule
    (true)
    =>
    (set-strategic-number sn-wood-gatherer-percentage 25)
    (set-strategic-number sn-food-gatherer-percentage 75)
    (set-strategic-number sn-gold-gatherer-percentage 0)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (chat-local-to-self "gather 25/75/0/0")
    (disable-self)
)
(defrule
    (civilian-population >= 10)
    =>
    (set-strategic-number sn-wood-gatherer-percentage 50)
    (set-strategic-number sn-food-gatherer-percentage 50)
    (set-strategic-number sn-gold-gatherer-percentage 0)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (chat-local-to-self "gather 50/50/0/0")
    (disable-self)
)
(defrule
    (civilian-population >= 20)
    =>
    (set-strategic-number sn-wood-gatherer-percentage 40)
    (set-strategic-number sn-food-gatherer-percentage 40)
    (set-strategic-number sn-gold-gatherer-percentage 20)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (chat-local-to-self "gather 40/40/20/0")
    (disable-self)
)
(defrule
    (civilian-population >= 20)
    (current-age >= castle-age)
    =>
    (set-strategic-number sn-wood-gatherer-percentage 25)
    (set-strategic-number sn-food-gatherer-percentage 40)
    (set-strategic-number sn-gold-gatherer-percentage 20)
    (set-strategic-number sn-stone-gatherer-percentage 15)
    (chat-local-to-self "gather 25/40/20/15")
    (disable-self)
)

; train villagers
(defrule
    (or (or (and (current-age == dark-age)(civilian-population < 25))
            (and (current-age == feudal-age)(civilian-population < 25))
        )
        (or (and (current-age == castle-age)(civilian-population < 100))
            (and (current-age == imperial-age)(civilian-population < 100))
        )
    )
    (or (or (and (building-type-count-total town-center == 1)(up-pending-objects c: villager < 2))
            (and (building-type-count-total town-center == 2)(up-pending-objects c: villager < 3))
        )
        (or (and (building-type-count-total town-center == 3)(up-pending-objects c: villager < 4))
            (and (building-type-count-total town-center >= 4)(up-pending-objects c: villager < 5))
        )
    )
    (can-train villager)
    =>
    (train villager)
)

; build houses if needed
(defrule
    (housing-headroom < 4) ; = current-housing-capacity - population
    (population-headroom > 3) ; = population-cap - current-housing-capacity
    (up-pending-objects c: house < 2) ; allow 2 houses to be build at the same time
    (can-build house)
    =>
    (build house)
)

; build mill
(defrule
    (building-type-count-total mill == 0)
    (up-pending-objects c: mill == 0)
    (can-build mill)
    (resource-found food) ; ensure we found the berries
    =>
    (build mill)
)
; -- implement mill upgrades

; build farm
(defrule
    (building-type-count-total lumber-camp > 0)
    (or 
        (unit-type-count-total villager > 15)
        (game-time > 600)
    )
    ; (building-type-count-total farm < 15) ; do we need this check?
    (idle-farm-count < 3)
    (up-pending-objects c: farm < 3)
    (can-build farm)
    =>
    (build farm)
)

; build lumber-camp and upgrade
(defrule
    (building-type-count-total mill > 0)
    (can-build-with-escrow lumber-camp)
    (resource-found wood)
    (dropsite-min-distance wood > 3)
    (up-pending-objects c: lumber-camp == 0)
    =>
    (release-escrow wood)
    (build lumber-camp)
    (up-modify-sn sn-camp-max-distance c:+ 3)
)
(defrule
    (can-research-with-escrow ri-double-bit-axe)
    =>
    (release-escrow food)
    (release-escrow wood)
    (research ri-double-bit-axe)
    (disable-self)
)
(defrule
    (can-research-with-escrow ri-two-man-saw)
    =>
    (release-escrow food)
    (release-escrow wood)
    (research ri-two-man-saw)
    (disable-self)
)
(defrule
    (can-research-with-escrow ri-bow-saw)
    =>
    (release-escrow food)
    (release-escrow wood)
    (research ri-bow-saw)
    (disable-self)
)

; mining camp (gold) and upgrade
(defrule
    (can-build-with-escrow mining-camp)
    (resource-found gold)
    (dropsite-min-distance gold > 3)
    (up-pending-objects c: mining-camp == 0)
    (unit-type-count-total villager >= 20)
    =>
    (release-escrow wood)
    (build mining-camp)
    (up-modify-sn sn-camp-max-distance c:+ 3)
)
(defrule
    (can-research-with-escrow ri-gold-mining)
    =>
    (release-escrow food)
    (release-escrow wood)
    (research ri-gold-mining)
    (disable-self)
)
(defrule
    (can-research-with-escrow ri-gold-shaft-mining)
    =>
    (release-escrow food)
    (release-escrow wood)
    (research ri-gold-shaft-mining)
    (disable-self)
)

; mining camp (stone)
(defrule
    (can-build mining-camp)
    (resource-found stone)
    (dropsite-min-distance stone > 3)
    (up-pending-objects c: mining-camp == 0)
    (unit-type-count-total villager >= 20)
    =>
    (build mining-camp)
    (up-modify-sn sn-camp-max-distance c:+ 3)
)

; build barracks
(defrule
    (can-build barracks)
    (building-type-count-total barracks == 0)
    (up-pending-objects c: barracks == 0)
    (building-type-count-total mill > 0)
    (building-type-count-total lumber-camp > 0)
    (building-type-count-total mining-camp > 0)
    =>
    (build barracks)
)
; research man-at-arms
(defrule
    (can-research-with-escrow ri-man-at-arms)
    =>
    (release-escrow food)
    (release-escrow gold)
    (research ri-man-at-arms)
    (disable-self)
)
; research squires
(defrule
    (can-research-with-escrow ri-squires)
    =>
    (release-escrow food)
    (research ri-squires)
    (disable-self)
)

; build blacksmith
(defrule
    (can-build blacksmith)
    (building-type-count-total blacksmith == 0)
    (up-pending-objects c: blacksmith == 0)
    =>
    (build blacksmith)
)

; build archery range (second feudal building)
(defrule
    (can-build archery-range)
    (building-type-count-total archery-range < 2)
    (up-pending-objects c: blacksmith < 2)
    =>
    (build archery-range)
)

; build market
(defrule
    (can-build market)
    (building-type-count-total market == 0)
    (up-pending-objects c: market == 0)
    =>
    (build market)
)

; build castle
(defrule
    (can-build castle)
    (building-type-count-total castle == 0)
    (up-pending-objects c: castle == 0)
    =>
    (build castle)
)
(defrule
    (can-build castle)
    (stone-amount > 1000) 
    (building-type-count-total castle > 0)
    (building-type-count-total castle < 6)
    (up-pending-objects c: castle == 0)
    =>
    (build-forward castle)
)

; research crossbowman
(defrule
    (can-research-with-escrow ri-crossbow)
    (unit-type-count-total archer-line > 2)
    =>
    (release-escrow food)
    (release-escrow gold)
    (research ri-crossbow)
    (disable-self)
)

; train militiaman
(defrule
    (goal g-train-militiaman-line yes)
    (up-pending-objects c: militiaman-line == 0)
    (food-amount > 200)
    (gold-amount > 80)
    (can-train militiaman-line)
    =>
    (train militiaman-line)
)

; train archer
(defrule
    (goal g-train-archer-line yes)
    (up-pending-objects c: archer-line < 2)
    (wood-amount > 75)
    (gold-amount > 135)
    (can-train archer-line)
    =>
    (train archer-line)
)

; feudal age
(defrule
    (unit-type-count-total villager >= 25)
    (can-research-with-escrow feudal-age)
    =>
    (release-escrow food)
    (research feudal-age)
    (disable-self)
)

; castle age
(defrule
    (unit-type-count-total villager >= 25)
    (can-research-with-escrow castle-age)
    =>
    (release-escrow food)
    (release-escrow gold)
    (research castle-age)
    (disable-self)
)

; imperial age
(defrule
    (unit-type-count-total villager >= 25)
    (can-research-with-escrow imperial-age)
    =>
    (release-escrow food)
    (release-escrow gold)
    (research imperial-age)
    (disable-self)
)

; attack
(defrule
    (true)
    =>
    (set-strategic-number sn-group-commander-selection-method 2)
    (set-strategic-number sn-percent-attack-soldiers 100)
    (set-strategic-number sn-group-form-distance 4)
    (set-strategic-number sn-attack-group-gather-spacing 1)
    (set-strategic-number sn-minimum-attack-group-size 24)
	(set-strategic-number sn-maximum-attack-group-size 500)
    (set-strategic-number sn-number-attack-groups 0)
    (disable-self)
)

; feudal rush: start training militiaman
(defrule
    (goal g-feudal-rush yes)
    =>
    (set-goal g-train-militiaman-line yes)
    (disable-self)
)

; feudal rush: attack when 4 militiaman
(defrule
    (goal g-feudal-rush yes)
    (unit-type-count-total militiaman-line >= 4)
    =>
    ; (attack-now)
    (set-strategic-number sn-minimum-attack-group-size 1)
	(set-strategic-number sn-maximum-attack-group-size 4)
    (set-strategic-number sn-number-attack-groups 4)

    (enable-timer t-stop-attack 120)
    (set-goal g-train-militiaman-line no)
    (set-goal g-train-archer-line yes)
    (chat-local-to-self "feudal rush: first wave")
    (disable-self)
)

; feudal rush: atttack when 6 archers
(defrule
    (goal g-feudal-rush yes)
    (unit-type-count-total archer-line >= 6)
    =>
    ; (attack-now)
    (set-strategic-number sn-minimum-attack-group-size 1)
	(set-strategic-number sn-maximum-attack-group-size 6)
    (set-strategic-number sn-number-attack-groups 6)

    (enable-timer t-stop-attack 120)
    (set-goal g-train-archer-line no)
    (set-goal g-feudal-rush no)
    (chat-local-to-self "feudal rush: second wave")
    (chat-local-to-self "feudal rush: completed")
    (disable-self)
)

; fast castle age
(defrule
    (goal g-fast-castle yes)
    (current-age >= castle-age)
    =>
    ; (set-strategic-number sn-camp-max-distance 24)
    (set-strategic-number sn-town-center-placement mining-camp)
    (chat-local-to-self "fast castle: reached castle age")
    (disable-self)
)

; fast castle; buy max 400 stone if needed
(defrule (goal g-fast-castle yes)(stone-amount < 100)(can-buy-commodity stone) => (buy-commodity stone)(disable-self))
(defrule (goal g-fast-castle yes)(stone-amount < 100)(can-buy-commodity stone) => (buy-commodity stone)(disable-self))
(defrule (goal g-fast-castle yes)(stone-amount < 100)(can-buy-commodity stone) => (buy-commodity stone)(disable-self))
(defrule (goal g-fast-castle yes)(stone-amount < 100)(can-buy-commodity stone) => (buy-commodity stone)(disable-self))

; fast castle: build town center
(defrule
    (goal g-fast-castle yes)
    (building-type-count-total town-center < 4)
    (up-pending-objects c: town-center < 4)
    (can-build town-center)
    =>
    (build town-center)
)

; fast castle complete
(defrule
    (goal g-fast-castle yes)
    (current-age >= castle-age)
    (building-type-count-total town-center >= 4)
    =>
    (set-goal g-fast-castle no)
    (set-goal g-enable-research yes)
    (chat-local-to-self "fast castle: complete")
    ; (set-strategic-number sn-camp-max-distance 100) ; default = 100
    (disable-self)
)

; stop attacking if timer triggers
(defrule
    (timer-triggered t-stop-attack)
    =>
    (disable-timer t-stop-attack)
    (up-reset-attack-now)
    (set-strategic-number sn-number-attack-groups 0)
)

; goals complete? train units and attack
(defrule
    (goal g-feudal-rush no)
    (goal g-fast-castle no)
    =>
    (set-strategic-number sn-minimum-attack-group-size 24)
	(set-strategic-number sn-maximum-attack-group-size 500)
    (set-strategic-number sn-number-attack-groups 4)
    (chat-local-to-self "sn-number-attack-groups = 1")
    (set-goal g-train-archer-line yes)
    (set-goal g-train-militiaman-line yes)
    (disable-self)
)