; enable fast castle if there are a lot of resources, e.g. deathmatch
(defrule
    (game-time < 30)
    (food-amount > 10000)
    (wood-amount > 10000)
    (gold-amount > 5000)
    (stone-amount > 2500)
    =>
    (set-goal g-fast-castle yes)
    (chat-local-to-self "fast castle: enabled (excessive resources detected)")
    (disable-self)
)

; research feudal asap
(defrule
    (goal g-fast-castle yes)
    (can-research-with-escrow feudal-age)
    =>
    (release-escrow food)
    (release-escrow wood)
    (research feudal-age)
)

; research castle asap
(defrule
    (goal g-fast-castle yes)
    (can-research-with-escrow castle-age)
    =>
    (release-escrow food)
    (release-escrow wood)
    (research castle-age)
)

; fast castle; buy max 400 stone if needed
(defrule (goal g-fast-castle yes)(stone-amount < 100)(can-buy-commodity stone) => (buy-commodity stone)(disable-self))
(defrule (goal g-fast-castle yes)(stone-amount < 100)(can-buy-commodity stone) => (buy-commodity stone)(disable-self))
(defrule (goal g-fast-castle yes)(stone-amount < 100)(can-buy-commodity stone) => (buy-commodity stone)(disable-self))
(defrule (goal g-fast-castle yes)(stone-amount < 100)(can-buy-commodity stone) => (buy-commodity stone)(disable-self))

; build 4 town centers
(defrule
    (goal g-fast-castle yes)
    (building-type-count-total town-center < 3)
    (up-pending-objects c: town-center < 2)
    (can-build town-center)
    =>
    (build town-center)
)

; done
(defrule
    (goal g-fast-castle yes)
    (current-age >= castle-age)
    (building-type-count-total town-center >= 3)
    =>
    (set-goal g-fast-castle no)
    (chat-local-to-self "fast castle: complete")
    (disable-self)
)