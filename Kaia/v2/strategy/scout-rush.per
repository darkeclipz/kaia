; https://www.youtube.com/watch?v=JsTNM7j6fs4

(defrule
    (goal g-scout-rush yes)
    =>
    (set-goal g-build no)
    (set-goal g-research no)
    (set-goal g-train no)
    (set-goal g-trade no)
    (set-goal g-attack no)
    (set-goal g-escrow no)
    (set-goal g-harass no)
    (set-goal g-build-order 0)
    (set-goal g-max-villagers-dark-age 23) ; max in dark age
    (set-goal g-max-villagers-feudal-age 34) ; max in feudal age
    (set-goal g-max-villagers-castle-age 120) ; max in castle age
    (set-goal g-max-villagers-imperial-age 120) ; max in imperial age
    (set-goal g-min-villagers-dark-age 23) ; needed before researching feudal age
    (set-goal g-min-villagers-feudal-age 34) ; needed before researching castle age
    (set-goal g-min-villagers-castle-age 50) ; needed before researching imperial age
    (set-strategic-number sn-home-exploration-time 300)
    (set-strategic-number sn-maximum-hunt-drop-distance 20)
    (chat-local-to-self "scout rush: enabled")
    (disable-self)
)

; gathering
(defrule
    (goal g-scout-rush yes)    
    (civilian-population >= 4)
    =>
    (set-strategic-number sn-food-gatherer-percentage 100)
    (set-strategic-number sn-wood-gatherer-percentage 0)
    (set-strategic-number sn-gold-gatherer-percentage 0)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (disable-self)
)
(defrule
    (goal g-scout-rush yes)    
    (civilian-population >= 5)
    =>
    (set-strategic-number sn-food-gatherer-percentage 100)
    (set-strategic-number sn-wood-gatherer-percentage 0)
    (set-strategic-number sn-gold-gatherer-percentage 0)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (disable-self)
)
(defrule
    (goal g-scout-rush yes)    
    (civilian-population >= 6)
    =>
    (set-strategic-number sn-food-gatherer-percentage 100)
    (set-strategic-number sn-wood-gatherer-percentage 0)
    (set-strategic-number sn-gold-gatherer-percentage 0)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (disable-self)
)
(defrule
    (goal g-scout-rush yes)    
    (civilian-population >= 10)
    =>
    (set-strategic-number sn-food-gatherer-percentage 60)
    (set-strategic-number sn-wood-gatherer-percentage 40)
    (set-strategic-number sn-gold-gatherer-percentage 0)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (disable-self)
)
(defrule
    (goal g-scout-rush yes)    
    (civilian-population >= 15)
    =>
    (set-strategic-number sn-food-gatherer-percentage 73)
    (set-strategic-number sn-wood-gatherer-percentage 27)
    (set-strategic-number sn-gold-gatherer-percentage 0)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (disable-self)
)
(defrule
    (goal g-scout-rush yes)    
    (civilian-population >= 20)
    =>
    (set-strategic-number sn-food-gatherer-percentage 60)
    (set-strategic-number sn-wood-gatherer-percentage 40)
    (set-strategic-number sn-gold-gatherer-percentage 0)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (disable-self)
)
(defrule
    (goal g-scout-rush yes)    
    (civilian-population >= 22)
    =>
    (set-strategic-number sn-food-gatherer-percentage 61)
    (set-strategic-number sn-wood-gatherer-percentage 30)
    (set-strategic-number sn-gold-gatherer-percentage 9)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (disable-self)
)
(defrule
    (goal g-scout-rush yes)    
    (civilian-population >= 28)
    =>
    (set-strategic-number sn-food-gatherer-percentage 42)
    (set-strategic-number sn-wood-gatherer-percentage 46)
    (set-strategic-number sn-gold-gatherer-percentage 12)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (disable-self)
)

; research feudal age
(defrule
    (goal g-scout-rush yes)    
    (civilian-population g:>= g-min-villagers-dark-age)
    (can-research feudal-age)
    =>
    (research feudal-age)
)

; research castle age
(defrule
    (goal g-scout-rush yes)
    (civilian-population g:>= g-min-villagers-feudal-age)
    (can-research castle-age)
    =>
    (research castle-age)
)

; train villagers; set g-max-villagers properly?
; (defrule
;     (goal g-scout-rush yes)
;     (current-age == dark-age)
;     (civilian-population < 22)
;     (up-pending-objects c: villager == 0)
;     (can-train villager)
;     =>
;     (train villager)
; )
; (defrule
;     (goal g-scout-rush yes)    
;     (current-age == feudal-age)
;     (civilian-population < 24)
;     (up-pending-objects c: villager == 0)
;     (can-train villager)
;     =>
;     (train villager)
; )

; house
(defrule
    (goal g-scout-rush yes)    
    (goal g-build-order 0)
    (can-build house)
    =>
    (up-get-point position-self g-x0)
    (up-set-target-point g-x0)
    (set-strategic-number sn-placement-zone-size 5)
    
    (up-build place-point 0 c: house)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build first house")
)

; house
(defrule
    (goal g-scout-rush yes)    
    (goal g-build-order 1)
    (building-type-count house == 1)
    (can-build house)
    =>
    (up-get-point position-self g-x0)
    (up-set-target-point g-x0)
    (set-strategic-number sn-placement-zone-size 5)
    (up-build place-point 0 c: house)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build second house")
)

; house
(defrule
    (goal g-scout-rush yes)    
    (goal g-build-order 2)
    (civilian-population >= 13)
    (can-build house)
    =>
    (up-get-point position-self g-x0)
    (up-set-target-point g-x0)
    (set-strategic-number sn-placement-zone-size 5)
    (up-build place-point 0 c: house)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build third house")
)

; house
(defrule
    (goal g-scout-rush yes)    
    (goal g-build-order 3)
    (building-type-count house == 3)
    (can-build house)
    =>
    (up-get-point position-self g-x0)
    (up-set-target-point g-x0)
    (set-strategic-number sn-placement-zone-size 5)
    (up-build place-point 0 c: house)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build fourth house")
)

; mill
(defrule
    (goal g-scout-rush yes)
    (goal g-build-order 4)
    (building-type-count house >= 4)
    (building-type-count-total mill == 0)
    (up-compare-goal g-build-mill == 0)
    (can-build mill)
    =>
    ; (up-full-reset-search)
    ; (up-get-point position-self g-x0)
    ; (up-filter-status c: status-resource c: list-active) ; berries
    ; (up-find-resource c: food c: 40)
    ; (up-clean-search search-remote object-data-distance search-order-asc)
    ; (up-set-target-object search-remote c: 1)
    ; (set-strategic-number sn-placement-zone-size 1)
    ; (up-build place-point 0 c: mill) ; near berries!
    (up-modify-goal g-build-mill c:+ 1)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build mill")
)

; lumber camp
(defrule
    (goal g-scout-rush yes)
    (up-compare-goal g-build-order == 5)
    (up-compare-goal g-wood-resources > 5)
    (up-pending-objects c: lumber-camp == 0)
    (building-type-count mill == 1)
    (can-build lumber-camp)
    =>
    ; find mill pos
    (up-full-reset-search)
    (up-find-local c: mill c: 1)
    (up-set-target-object search-local c: 0)
    (up-get-object-data object-data-point-x g-x0)
    (up-get-object-data object-data-point-y g-y0)
    (up-full-reset-search)
    ; (up-get-point position-self g-x0)
    ; (up-get-point position-center g-x1)
    ; (up-lerp-tiles g-x0 g-x1 c: -)
    (up-set-target-point g-x0)
    (up-filter-status c: status-ready c: list-active)
    ; (up-filter-distance c: 0 c: 20)
	(up-find-resource c: wood c: 40)
    (up-get-search-state g-s)
    ; (up-modify-goal g-wood-resources g:= g-rt)
    (up-clean-search search-remote object-data-distance search-order-asc)
    ; (up-set-target-object search-remote g: 5) ; skip first trees, can be out of bounds!!
    (up-set-target-object search-remote c: 5)
    (up-get-object-data object-data-point-x g-x0)
    (up-get-object-data object-data-point-y g-y0)
    (up-set-target-point g-x0)
    (set-strategic-number sn-placement-zone-size 1)
    (up-build place-point 0 c: lumber-camp)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build lumber camp 1")
)

; lumber camp (dont increment build order)
(defrule
    (goal g-scout-rush yes)
    (up-compare-goal g-build-order > 5)
    (dropsite-min-distance wood > 5)
    (up-compare-goal g-wood-resources > 5)
    (up-pending-objects c: lumber-camp == 0)
    (building-type-count mill >= 1)
    (can-build lumber-camp)
    =>
    (up-full-reset-search)
    (up-filter-status c: status-ready c: list-active)
    (up-get-point position-self g-x0)
    (up-set-target-point g-x0)
	(up-find-resource c: wood c: 40)
    (up-get-search-state g-s)
    (up-modify-goal g-wood-resources g:= g-rt)
    (up-clean-search search-remote object-data-distance search-order-asc)
    ; (up-set-target-object search-remote g:- g-stragglers) ; skip first trees, can be out of bounds!!
    (up-set-target-object search-remote c: 5)
    (up-get-object-data object-data-point-x g-x0)
    (up-get-object-data object-data-point-y g-y0)
    (up-set-target-point g-x0)
    (set-strategic-number sn-placement-zone-size 1)
    (up-build place-point 0 c: lumber-camp)
    (chat-local-to-self "build lumber camp 2")
)

; house
(defrule
    (goal g-scout-rush yes)    
    (goal g-build-order 6)
    (building-type-count lumber-camp == 1)
    (can-build house)
    =>
    (up-get-point position-self g-x0)
    (up-set-target-point g-x0)
    (set-strategic-number sn-placement-zone-size 5)
    (up-build place-point 0 c: house)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build fourth house")
)

; build farms (feudal age pending)
(defrule
    (goal g-scout-rush yes)    
    (up-compare-goal g-build-order > 6)
    (up-research-status c: feudal-age >= research-pending)
    (building-type-count-total farm < 14)
    (idle-farm-count == 0)
    (building-type-count-total stable > 0)
    (can-build farm)
    =>
    (build farm)
    (chat-local-to-self "build farm (first 4)")
    ; (up-modify-goal g-build-order c:+ 1)
)

; build farms (castle age pending)
(defrule
    (goal g-scout-rush yes)    
    (up-compare-goal g-build-order > 6)
    (up-research-status c: castle-age >= research-pending)
    (building-type-count-total farm < 22)
    (up-pending-objects c: farm < 3)
    (can-build farm)
    =>
    (build farm)
    (chat-local-to-self "build farm")
)

; mining camp
(defrule
    (goal g-scout-rush yes)
    (goal g-build-order 7)
    (up-compare-goal g-gold-resources > 0)
    (civilian-population >= 22)
    (can-build mining-camp)
    =>
    (up-full-reset-search)
    (up-filter-status c: status-resource c: list-active)
	(up-find-resource c: gold c: 40)
    (up-get-search-state g-s)
    (up-modify-goal g-gold-resources g:= g-rt)
    (up-get-point position-self g-x0)
    (up-set-target-point g-x0)
    (up-clean-search search-remote object-data-distance search-order-asc)
    (up-set-target-object search-remote c: 0)
    (up-get-object-data object-data-point-x g-x0)
    (up-get-object-data object-data-point-y g-y0)
    (up-set-target-point g-x0)
    (set-strategic-number sn-placement-zone-size 1)
    (up-build place-point 0 c: mining-camp)
    (up-modify-goal g-build-order c:+ 2) ; !!!!
    (chat-local-to-self "build mining camp")
)

; dark age building
(defrule
    (goal g-scout-rush yes)    
    (goal g-build-order 9)
    (can-build barracks)
    =>
    (up-set-placement-data my-player-number town-center c: 4) ; bias to front
    (set-strategic-number sn-placement-zone-size 6)
    (set-strategic-number sn-placement-to-center 1)
    (up-build place-control 0 c: barracks)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build barracks")
)

; send scout back to help defend against rush
(defrule
    (goal g-scout-rush yes)
    (up-compare-goal g-build-order >= 9)
    =>
	(set-strategic-number sn-number-explore-groups 0)
	(set-strategic-number sn-total-number-explorers 0)
    (up-reset-scouts)
    (chat-local-to-self "recall scout")
    (disable-self)
)

; tower?
(defrule
    (goal g-scout-rush yes)
    (up-compare-goal g-build-order > 9)
    (building-type-count-total watch-tower == 0)
    (up-pending-objects c: watch-tower == 0)
    (can-build watch-tower)
    =>
    (up-get-point position-self g-x0) ; my pos
    (up-get-point position-center g-x1) ; center
    (up-lerp-tiles g-x0 g-x1 g: g-town-size) ; lerp my pos some tiles towards the center
    (up-set-target-point g-x0) ; try place at that pos
    (set-strategic-number sn-placement-zone-size 1)
    (up-build place-point 0 c: watch-tower-line)
    (disable-self)
)

; first feudal building
(defrule
    (goal g-scout-rush yes)    
    (goal g-build-order 10)
    (can-build stable)
    =>
    (up-set-placement-data my-player-number town-center c: -4) ; bias to back
    (set-strategic-number sn-placement-zone-size 6)
    (set-strategic-number sn-placement-to-center 1)
    (up-build place-control 0 c: stable)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build stable")
)

; second feudal building
(defrule
    (goal g-scout-rush yes)    
    (goal g-build-order 11)
    (can-build archery-range)
    =>
    (up-set-placement-data my-player-number town-center c: -4) ; bias to back
    (set-strategic-number sn-placement-zone-size 6)
    (set-strategic-number sn-placement-to-center 1)
    (up-build place-control 0 c: archery-range)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build archery range")
)

; train scouts to defend
(defrule
    (goal g-scout-rush yes)
    (food-amount > 85)
    (unit-type-count-total scout-cavalry-line < 3)
    (up-pending-objects c: scout-cavalry-line == 0)
    ; (up-research-status c: castle-age >= research-pending)
    (can-train scout-cavalry-line)
    =>
    (train scout-cavalry-line)
)

; train archers to defend
(defrule
    (goal g-scout-rush yes)
    (food-amount > 85)
    (unit-type-count-total archer-line < 3)
    (up-pending-objects c: archer-line == 0)
    ; (up-research-status c: castle-age >= research-pending)
    (can-train archer-line)
    =>
    (train archer-line)
)

; buy 100 food
(defrule
    (up-compare-goal g-build-order >= 11)
    (food-amount < 800)
    (food-amount > 700)
    (gold-amount > 350)
    (up-research-status c: feudal-age == research-available)
    (current-age == feudal-age)
    (can-buy-commodity food)
    =>
    (buy-commodity food)
    (chat-local-to-self "bought food")
    (disable-self)
)

; buy 100 wood
(defrule
    (up-compare-goal g-build-order >= 11)
    (current-age == castle-age)
    (can-buy-commodity wood)
    =>
    (buy-commodity wood)
    (chat-local-to-self "bought wood")
    (disable-self)
)

; town center 1
(defrule
    (goal g-scout-rush yes)    
    (up-compare-goal g-build-order >= 12)
    (building-type-count-total town-center < 2)
    (up-pending-objects c: town-center == 0)
    (can-build town-center)
    =>
    (set-strategic-number sn-town-center-placement mining-camp)
    (up-build place-normal 0 c: town-center)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build town center 1")
)

; town center 2
(defrule
    (goal g-scout-rush yes)
    (up-compare-goal g-build-order >= 13)
    (building-type-count-total town-center >= 2)
    (building-type-count-total town-center < 3)
    (up-pending-objects c: town-center < 2)
    (can-build town-center)
    =>
    (set-strategic-number sn-town-center-placement lumber-camp)
    (up-build place-normal 0 c: town-center)
    (up-modify-goal g-build-order c:+ 1)
    (chat-local-to-self "build town center 2")
)

; auto build houses
(defrule
    (goal g-scout-rush yes)
    (housing-headroom < 5)
    (population-headroom > 3) ; = population-cap - current-housing-capacity
    (up-compare-goal g-build-order >= 7)
    (up-pending-objects c: house == 0)
    (can-build house)
    =>
    ; try to place the houses as close to the TC as possible
    (up-get-point position-self g-x0)
    (up-set-target-point g-x0)
    (set-strategic-number sn-placement-zone-size 5)
    (up-build place-point 0 c: house)
)

; research bow saw
(defrule
    (up-research-status c: castle-age >= research-pending)
    (can-research ri-bow-saw)
    =>
    (research ri-bow-saw)
)

; research heavy plow
(defrule
    (up-research-status c: castle-age >= research-pending)
    (can-research ri-heavy-plow)
    =>
    (research ri-heavy-plow)
)

; fast castle completed
(defrule
    (goal g-scout-rush yes)
    (current-age >= castle-age)
    (building-type-count-total town-center >= 3)
    =>
    (set-goal g-build yes)
    (set-goal g-research yes)
    (set-goal g-train yes)
    (set-goal g-train-reset yes)
    (set-goal g-trade yes)
    (set-goal g-attack yes)
    (set-goal g-escrow yes)
    (set-goal g-scout-rush no)
    (set-goal g-scout yes)
	(set-strategic-number sn-number-explore-groups 1)
	(set-strategic-number sn-total-number-explorers 1)
    (chat-local-to-self "scout rush: done")
    (disable-self)
)




; finish knights!

(defrule 
    (current-age == imperial-age)
    =>
    (set-goal g-research no)
    ; (release-escrow wood)
    (set-goal g-release-escrow no)
    (disable-self)
)

; (defrule
;     (goal g-escrow yes)
;     (current-age == imperial-age)
;     =>
;     (set-escrow-percentage food 50)
;     (set-escrow-percentage gold 50) ; release all at once to prevent only one type of unit being trained
; )

; upgrade knights; increases attack and hit points by 20%
; 300 food
; 300 gold
(defrule
    ; (goal g-research yes)
    (unit-type-count-total knight-line >= min-units-required-for-upgrade)
    (can-research-with-escrow ri-cavalier)
    =>
    (release-escrow food)
    (release-escrow gold)
    (research ri-cavalier)
    (disable-self)
)

; upgrade cavalier; increases attack and hit points by another 20%?
; 1300 food
; 750 gold
(defrule
    ; (goal g-research yes)
    (unit-type-count-total knight-line >= min-units-required-for-upgrade)
    (can-research-with-escrow ri-paladin)
    =>
    (release-escrow food)
    (release-escrow gold)
    (research ri-paladin)
    (disable-self)
)

(defrule
    (up-research-status c: ri-paladin >= research-pending)
    =>
    (set-goal g-research yes)
    (set-goal g-release-escrow yes)
    (disable-self)
)